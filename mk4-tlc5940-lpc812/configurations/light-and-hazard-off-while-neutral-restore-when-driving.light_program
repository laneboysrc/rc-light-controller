// Light program that turns the lights and hazard off
// when the car is in neutral for more than 5 seconds.
// When the car is then driving again the lights and
// hazard is restored.
//
// The restoring is cancelled if the light switch or
// hazard are operated after having been turned off
run when neutral
// This light program runs when the throttle is in
// neutral (= not in forward, braking, reversing)

// is_saved is used as flag whether the light switch
// and hazard have been saved.
global var is_saved
global var saved_light_switch_position
global var saved_hazard

  // Wait 5 seconds. If the car is driving off
    // within these 5 seconds the light program is
    // cancalled and nothing happens.
  sleep 5000

    // 5 seconds over: save light switch and hazard,
    // and set the flag that indicates that the
    // settings have been stored
    saved_light_switch_position = light-switch-position
    saved_hazard = 0
    if is hazard
    saved_hazard = 1
    is_saved = 1

    // Turn off the light switch and hazard
    hazard = 0
    light-switch-position = 0

    // Monitor light switch and hazard while the car is in
    // neutral. If they are changed by the user can cancel
    // the restoration when driving off later by clearing
    // the is_saved flag
loop:
  sleep 20
    if light-switch-position != 0
    goto save_cancelled
    if is hazard
    goto save_cancelled
    goto loop

save_cancelled:
  is_saved = 0
end

run when forward or reversing or braking
  // This light program runs when the throttle is in
  // forward, braking or reversing (but not neutral)

    // If the flag is 0 we don't restore anything
  if is_saved == 0
    goto exit

    // Restore the light switch and hazard and clear the flag
    light-switch-position = saved_light_switch_position
    hazard = saved_hazard
    is_saved = 0

exit:

end
