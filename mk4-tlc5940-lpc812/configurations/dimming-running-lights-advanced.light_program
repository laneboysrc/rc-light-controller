run always

const BRIGHT = 100          // Light on brightness value
const DIM = 10              // Light dim brightness value (when indicator active)

const FADE_UP = 5           // Fade-to-bright slowly
const FADE_DOWN = 5         // Fade-to-off slowly
const FADE_DIM = 15         // Fade-to-dim faster

const DIM_UP_DELAY = 50     // times 20 ms = 1 second

led light = led[4]
var dim_up_delay

loop:
    sleep 20

    // If the light switch is off, turn the light off no matter what else is going on
    if light-switch-position == 0
    goto light-off

    // If the indicator or hazard is active then dim the light
    if is indicator-left
    goto light-dim
    if is hazard
    goto light-dim

    // The light is on and indicator/hazard are inactive.
    // If the variable dim_up_delay is 0 then we turn the
    // light on
    if dim_up_delay == 0
    goto light-on

    // If we were dim just now because of indicator/hazard
    // active, we decrement the dim_up_variable. Together
    // with the sleep 20 at the being of the loop this
    // sets the delay before the lights come back up
    // after indicator/hazard
    dim_up_delay -= 1
    goto loop

light-on:
    fade light stepsize FADE_UP
    light = 100
    goto loop

light-off:
    fade light stepsize FADE_DOWN
    light = 0
    dim_up_delay = 0
    goto loop

light-dim:
    fade light stepsize FADE_DIM
    light = DIM
    // Prime the delay time before dimming back up
    // once the indicator/hazard is off.
    dim_up_delay = DIM_UP_DELAY
    goto loop

end


// Same as above, but for the right side
run always

const BRIGHT = 100          // Light on brightness value
const DIM = 10              // Light dim brightness value (when indicator active)

const FADE_UP = 5           // Fade-to-bright slowly
const FADE_DOWN = 5         // Fade-to-off slowly
const FADE_DIM = 15         // Fade-to-dim faster

const DIM_UP_DELAY = 50     // times 20 ms = 1 second

led light = led[5]
var dim_up_delay

loop:
    sleep 20

    if light-switch-position == 0
    goto light-off

    if is indicator-right
    goto light-dim
    if is hazard
    goto light-dim

    if dim_up_delay == 0
    goto light-on

    dim_up_delay -= 1
    goto loop

light-on:
    fade light stepsize FADE_UP
    light = 100
    goto loop

light-off:
    fade light stepsize FADE_DOWN
    light = 0
    dim_up_delay = 0
    goto loop

light-dim:
    fade light stepsize FADE_DIM
    light = DIM
    dim_up_delay = DIM_UP_DELAY
    goto loop

end
