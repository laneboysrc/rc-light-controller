// Manual indicators on an analog stick on AUX2, with ST hold
//
// This light program implements indicators that are controlled
// by an analog stick (e.g. left horizonal gimbal on a transmitter) that
// returns to center.
//
// The indicators blink as long as the stick is held in the direction.
// When the stick is released they continue to blink for a certain
// amount of time.
//
// The indicators keep blinking in the same direction if the steering input
// is held in the same direction. So you can start the indicator by tapping
// AUX2, keep driving the corner using the steering wheel, and the indicators
// will only stop after you returned the steering to neutral (or the opposite
// direction)
//
// Note that we also have to manually implement the hazard lights since
// we are taking over the indicator LEDs.
run always

// Brightness value used for the indicators when they light up
const BRIGHTNESS = 100

// Continue blinking the indicators for x seconds after the stick
// has returned to neutral
const INDICATOR_TIMEOUT = 3500

// Threshold and hysteresis for detecting stick movement
const STICK_LEFT_ON = -50
const STICK_LEFT_OFF = -33
const STICK_RIGHT_ON = 50
const STICK_RIGHT_OFF = 33

led indicator-front-l = led[6]
led indicator-front-r = led[7]
led indicator-rear-l = led[12]
led indicator-rear-r = led[13]

// Variable that holds the stick value, because the aux, aux2 and aux3
// values can not be directly used in "skip if" instructions.
var stick-position
var steering-position

// Variable and state definitions for a simple state machine handling
// the various indicator states.
var state
const IND_OFF = 0
const IND_LEFT = 1
const IND_RIGHT = 2
const IND_LEFT_DELAY = 3
const IND_RIGHT_DELAY = 4
const IND_WAIT_FOR_OFF = 5

var timeout

    // Set the initial state to OFF
    state = IND_OFF

loop:
    // Let the rest of the light controller run for 20 ms before
    // executing the next loop of this script.
    sleep 0

    // Load the current value of the AUX2 channel into our local
    // variable
    stick-position = aux2
    steering-position = steering

    // If the hazard lights are on we always blink them regardless
    // of the indicator state.
    skip if not hazard
    goto hazard

    skip if state != IND_OFF
    goto indicator-off
    skip if state != IND_LEFT
    goto indicator-left
    skip if state != IND_RIGHT
    goto indicator-right
    skip if state != IND_LEFT_DELAY
    goto indicator-left-delay
    skip if state != IND_RIGHT_DELAY
    goto indicator-right-delay
    skip if state != IND_WAIT_FOR_OFF
    goto indicator-wait-for-off

    // None of the above values: should never happen...
    // If it does, we reset the state
    state = IND_OFF
indicator-off:
    // If the stick-position is moved to the LEFT, turn the left indicator on
    skip if stick-position > STICK_LEFT_ON
    state = IND_LEFT
    // If the stick-position is moved to the RIGHT, turn the right indicator on
    skip if stick-position < STICK_RIGHT_ON
    state = IND_RIGHT
    // fall through

lights-off:
    indicator-front-l, indicator-front-r, indicator-rear-l, indicator-rear-r = 0
    goto loop

indicator-left:
    // If the stick is held left continue blinking left regardless what
    // the steering wheel does
    skip if stick-position > STICK_LEFT_OFF
    goto blink-left

    // If the stick has been moved to the far-right then turn the indicators
    // off immediately, regardless what the steering wheel does
    skip if stick-position < STICK_RIGHT_ON
    goto turn-indicator-off

    // If we reach here the stick has been moved to the center

    // If the steering is held left, then continue blinking
    skip if steering-position > STICK_LEFT_OFF
    goto blink-left

    // If the stick has been moved to the far-right then turn the indicators
    // off immediately,
    skip if steering-position < STICK_RIGHT_ON
    goto turn-indicator-off

    // If the stick has been moved to the center continue flashing
    // the indicators for a certain time

    // goto left-indicator-delay
    // fall through

left-indicator-delay:
    state = IND_LEFT_DELAY
    timeout = INDICATOR_TIMEOUT
    goto blink-left

turn-indicator-off:
    state = IND_WAIT_FOR_OFF
    goto lights-off

indicator-right:
    // If the stick is held right continue blinking left regardless what
    // the steering wheel does
    skip if stick-position < STICK_RIGHT_OFF
    goto blink-right

    // If the stick has been moved to the far-left then turn the indicators
    // off immediately, regardless what the steering wheel does
    skip if stick-position > STICK_LEFT_ON
    goto turn-indicator-off

    // If we reach here the stick has been moved to the center

    // If the steering is held right, then continue blinking
    skip if steering-position < STICK_RIGHT_OFF
    goto blink-right

    // If the stick has been moved to the far-left then turn the indicators
    // off immediately,
    skip if steering-position > STICK_LEFT_ON
    goto turn-indicator-off

    // If the stick has been moved to the center continue flashing
    // the indicators for a certain time

    // goto left-indicator-delay
    // fall through

right-indicator-delay:
    state = IND_RIGHT_DELAY
    timeout = INDICATOR_TIMEOUT
    goto blink-right

indicator-left-delay:
    // If the stick has been moved to the far-right then turn the indicators
    // off immediately
    skip if stick-position < STICK_RIGHT_ON
    goto turn-indicator-off

    // Decrement the timeout (this code runs every 20 ms), if the
    // timeout has expired turn the indicator off
    timeout -= 20
    skip if timeout <= 0
    goto blink-left
    goto turn-indicator-off

indicator-right-delay:
    // If the stick has been moved to the far-leften turn the indicators
    // off immediately
    skip if stick-position > STICK_LEFT_ON
    goto turn-indicator-off

    // Decrement the timeout (this code runs every 20 ms), if the
    // timeout has expired turn the indicator off
    timeout -= 20
    skip if timeout <= 0
    goto blink-right
    goto turn-indicator-off

indicator-wait-for-off:
    // Wait for the stick to have returned to neutral
    skip if stick-position > STICK_LEFT_OFF
    goto loop
    skip if stick-position > STICK_RIGHT_OFF
    state = IND_OFF
    goto loop

blink-left:
    skip if is blink-flag
    goto lights-off
    indicator-front-l, indicator-rear-l = BRIGHTNESS
    goto loop

blink-right:
    skip if is blink-flag
    goto lights-off
    indicator-front-r, indicator-rear-r = BRIGHTNESS
    goto loop

hazard:
    skip if is blink-flag
    goto lights-off
    goto hazard-on

hazard-on:
    indicator-front-l, indicator-front-r, indicator-rear-l, indicator-rear-r = BRIGHTNESS
    goto loop

end